@pre  true
@post true
int[] MergeSort(int[] a)
{
	return ms(a, 0, |a| - 1);
}

@pre  l >= 0 && u >= l - 1
@post true
# (u - l + 1)
int[] ms(int[] a_0, int l, int u) {
	int[] a := a_0;
	if (l >= u) {
		return a;
	}
	else {
		int m := (l + u) div 2;
		a := ms(a, l, m);
		a := ms(a, m + 1, u);
		a := merge(a, l, m, u);
		return a;
	}
}

@pre  l >= 0 && u >= 0 && u >= l
@post true
int[] merge(int[] a_0, int l, int m, int u) {
	int[] buf := new int[u-l+1];
	int[] a := a_0;
	int i := l;
	int j := m + 1;
	for 
		@ l >= 0 && u >= 0 && k <= u - l + 1 && u >= l
		# (2, u - l + 1 - k)
		(int k := 0; k < u - l + 1; k := k + 1)
	{
		if (i > m) {
			buf[k] := a_0[j];
			j := j + 1;
		}
		else if (j > u) {
			buf[k] := a_0[i];
			i := i + 1;
		}
		else if (a_0[i] <= a_0[j]) {
			buf[k] := a_0[i];
			i := i + 1;
		}
		else {
			buf[k] := a_0[j];
			j := j + 1;
		}
	}
	for 
		@ l >= 0 && u >= 0 && k <= u - l + 1
		# (1, u - l + 1 - k)
		(int k := 0; k < u - l + 1; k := k + 1)
	{
		a[l + k] := buf[k];
	}

	return a;
}
